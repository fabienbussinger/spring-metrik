buildscript {
    repositories { mavenCentral() }
}

plugins {
    id 'io.spring.dependency-management' version '1.0.0.RELEASE'
    id 'org.sonarqube' version '2.6'
    id 'net.researchgate.release' version '2.6.0'
}

ext { springBootVersion = '1.5.10.RELEASE' }

group = 'io.github.tibus29'

apply plugin: 'java'
apply plugin: 'jacoco'

if(project.hasProperty('ossrhUsername')) {
    apply from: 'gradle/publish.gradle'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories { mavenCentral() }

dependencies {

    compile 'javax.inject:javax.inject:1'
    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'commons-beanutils:commons-beanutils:1.9.3'

    testCompile 'org.awaitility:awaitility:3.0.0'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}

release {
    preTagCommitMessage = '[release] - releasing '
    tagCommitMessage = '[release] - '
    newVersionCommitMessage = '[release] - new version '
    tagTemplate = 'v${version}'
    scmAdapters = [ net.researchgate.release.GitAdapter ]
}

jacocoTestReport {

    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacoco/html")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

jacocoTestReport.dependsOn test
jacocoTestCoverageVerification.dependsOn jacocoTestReport

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '4.5'
}